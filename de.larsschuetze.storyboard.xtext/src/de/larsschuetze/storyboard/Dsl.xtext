grammar de.larsschuetze.storyboard.Dsl
with org.eclipse.xtext.xbase.Xbase

generate dsl "http://www.larsschuetze.de/storyboard/Dsl"

Storyboard:
	'events' '{'
		importedEvents += Event+
	'}'
	'storyboard' name = QualifiedName
	elements += AbstractElement*
;

AbstractElement:
	Node | Transition
;

Node:
	ControlNode | StoryPatternNode
;

ControlNode:
	StartNode | EndNode
;

StartNode:
	'start' name = ValidID
;

EndNode:
	'end' name = ValidID
;

Event:
	'import' eventType = JvmTypeReference 'as' name = ValidID
;

Transition:
	'transition' name = ValidID '{'
		'{' event = [Event] guard = Guard? '}'
		source = [Node]('.'sourcePort = StoryPatternPort)? '->' target = [Node]
	'}'
;

Guard:
	'[' guard = GuardValue ']'
;

GuardValue:
	(ID | STRING | INT | '(' | ')' | '.' | OpCompare | OpEquality)+
;

StoryPatternPort:
	SuccessPort | FailurePort
;

FailurePort:
	'failure'
;

SuccessPort:
	'success'	
;

StoryPatternNode:
	'storypattern' name = QualifiedName
	'{'
		'class' className = QualifiedName '{'
			(roleReconfigurations += RoleNode)+
		'}'
	'}'
;

RoleNode:
	(ConditionRoleNode | RewriteRoleNode) 'in' compartmentName = QualifiedName
;

RewriteRoleNode:
	AddRoleNode | RenewRoleNode | RemoveRoleNode
;

RemoveRoleNode:
	'remove' name = QualifiedName
;

RenewRoleNode:
	'renew' name = QualifiedName
;

AddRoleNode:
	'add' name = QualifiedName
;

ConditionRoleNode:
	PlayRoleNode | ProhibitedRoleNode
;

ProhibitedRoleNode:
	'prohibits' name = QualifiedName
;

PlayRoleNode:
	'plays' name = QualifiedName
;

